dnl Welcome to the Sphinx automated build system.
dnl try not to hurt yourself ;)

AC_INIT(sphinxbase, 0.2)
AC_CONFIG_HEADERS([include/config.h include/sphinx_config.h])
AM_INIT_AUTOMAKE([no-define dist-bzip2])

CFLAGS=${CFLAGS:--g -O2 -Wall}

AC_CANONICAL_HOST

dnl
dnl Set proper compiler flags and such for some platforms
dnl
case $host in
     arm-wince-pe*)
	CFLAGS=${CFLAGS:--march=armv4 -mapcs-32 -malignment-traps}
	LIBS='-lc -lgcc -lwinsock -lcoredll'
	CFLAGS="$CFLAGS -DNEWLIB -DSARM -DWIN32 -DGNUWINCE"
	;;
     *uclinux*)
	# uClinux needs special "flat" binaries
	LDFLAGS="$LDFLAGS -Wl,-elf2flt"
	;;
     *)
     ;;
esac     

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
AC_WORDS_BIGENDIAN
AC_TYPE_SIGNAL
AC_CHECK_TYPES(long long)
AC_CHECK_SIZEOF(long long)

dnl
dnl Check for Lapack stuff unless disabled
dnl
use_lapack=true
use_internal_lapack=false
AC_ARG_WITH(lapack,
	AC_HELP_STRING([--without-lapack],
		       [Disable matrix algebra support (depends on LAPACK)]),[
if test x$withval = xno; then
   use_lapack=false
fi
])
if test x$use_lapack = xtrue; then
   AC_CHECK_LIB(m, log)
   AC_CHECK_LIB(g2c, r_log)
   AC_CHECK_LIB(blas, sgemm_)
   AC_CHECK_LIB(lapack, sgesv_,,use_internal_lapack=true)
   AC_DEFINE(WITH_LAPACK, [], [Enable matrix algebra with LAPACK])
fi
AM_CONDITIONAL(LAPACK_LITE, test x$use_internal_lapack = xtrue)

dnl
dnl Allow compilation for fixed or floating-point MFCC and GMM computation
dnl
AC_ARG_ENABLE(fixed,
	AC_HELP_STRING([--enable-fixed=RADIX],
		       [Use 32-bit fixed-point for MFCC and GMM computation,
		        optionally specifying a radix point]),[
if test x$enableval = xyes; then
   AC_DEFINE(FIXED_POINT)
else
   AC_DEFINE(FIXED_POINT, [], [Use fixed-point computation])
   AC_DEFINE_UNQUOTED(DEFAULT_RADIX,$enableval,[Default radix point for fixed-point])
fi])

dnl
dnl Enable 16-bit fixed-point (Q15) format for MFCC (less accurate, more fast)
dnl
AC_ARG_ENABLE(fixed16,
	AC_HELP_STRING([--enable-fixed16],
		       [Use 16-bit fixed-point for MFCC computation]),[
if test x$enableval = xyes; then
   AC_DEFINE(FIXED_POINT, [], [Use fixed-point computation])
   AC_DEFINE(FIXED16, [], [Use Q15 fixed-point computation])
fi])

dnl
dnl determine audio type or use none if none supported on this platform
dnl

ad_files=""
ad_libs=""

dnl Added from suggestion by 
dnl Jasper van Veghel <jvveghel@vanboxtelsoftware.nl>, 02/03/2003:
AC_ARG_WITH(alsa,
           AC_HELP_STRING([--with-alsa], [Use ALSA library for sound I/O]),
           [ad_files="ad_alsa.lo"
            ad_backend="AD_BACKEND_ALSA"
            ad_libs="-lasound"
	    AC_DEFINE(AD_BACKEND_ALSA, [], [Use ALSA library for sound I/O])
            AC_CHECK_HEADER(alsa/asoundlib.h,,
                    AC_ERROR(ALSA header file <alsa/asoundlib.h> not found!))]
)

if test "x$ad_files" = "x"; then
        case $host in
                *-*-linux*)
                        ad_files="ad_oss.lo"
                        ad_backend="AD_BACKEND_OSS"
			AC_DEFINE(AD_BACKEND_OSS, [], [Use OSS interface for sound I/O])
                        ;;
                # FIXME: isn't this the same OSS as on Linux?
                *-*-freebsd*|*-*-netbsd*|*-*-openbsd*)
                        ad_files="ad_oss_bsd.lo mulaw_base.lo"
                        ad_libs="-lossaudio"
                        ad_backend="AD_BACKEND_OSS_BSD"
			AC_DEFINE(AD_BACKEND_OSS_BSD, [], [Use OSS interface for sound I/O])
                        ;;
                *-*-osf*)
                        ad_files="ad_osf.lo mulaw_base.lo"
                        ad_backend="AD_BACKEND_OSF"
			AC_DEFINE(AD_BACKEND_OSF, [], [Use OSF interface for sound I/O])
                        ;;
                *-*-irix*)
                        ad_files="ad_irix.lo mulaw_base.lo"
                        ad_libs="-laudio"
                        ad_backend="AD_BACKEND_IRIX"
			AC_DEFINE(AD_BACKEND_IRIX, [], [Use IRIX interface for sound I/O])
                        ;;
                *-*-sunos4*)
                        ad_files="ad_sunos.lo audio_utils_sunos.lo"
                        ad_libs="-lm -lnsl -ldl"
                        ad_backend="AD_BACKEND_SUNOS"
			AC_DEFINE(AD_BACKEND_SUNOS, [], [Use SunOS interface for sound I/O])
                        ;;
                *-*-solaris*)
                        ad_files="ad_sunos.lo audio_utils_sunos.lo"
                        ad_libs="-lm -lnsl -lsocket -ldl"
                        ad_backend="AD_BACKEND_SUNOS"
			AC_DEFINE(AD_BACKEND_SUNOS, [], [Use SunOS interface for sound I/O])
                        ;;
                *-*-*cygwin*)
                        ad_files="play_win32.lo rec_win32.lo"
                        ad_libs="-lwinmm"
                        ad_backend="AD_BACKEND_WIN32"
			AC_DEFINE(AD_BACKEND_WIN32, [], [Use WinMM interface for sound I/O])
                        ;;
                *-*apple*-*)
                        ad_files="ad_base.lo"
                        ad_backend="AD_BACKEND_NONE"
			AC_DEFINE(AD_BACKEND_NONE, [], [No audio interface for Mac OS (yet)])
                        ;;
                *)
                        ad_files="ad_base.lo"
                        ad_backend="AD_BACKEND_NONE"
                        AC_MSG_RESULT([No audio interface for host type $host.])
			AC_DEFINE(AD_BACKEND_NONE, [], [No interface for sound I/O])
                        ;;
        esac
fi

AC_PATH_PROG(PERL,perl)
AC_PATH_PROG(CSH,csh)
AC_SUBST(ad_files)
AC_SUBST(ad_libs)
AC_SUBST(ad_backend)
AC_SUBST(PERL)
AC_SUBST(CSH)

AM_PROG_LIBTOOL

AC_OUTPUT([
Makefile
include/Makefile
src/Makefile
src/libsphinxfe/Makefile
src/libsphinxfeat/Makefile
src/libsphinxad/Makefile
src/libsphinxutil/Makefile
src/sphinx_fe/Makefile
src/sphinx_cepview/Makefile
doc/Makefile
test/Makefile
test/unit/Makefile
test/unit/test_case/Makefile
test/unit/test_hash/Makefile
test/unit/test_matrix/Makefile
test/unit/test_feat/Makefile
test/regression/Makefile
test/regression/test-cepview.sh
test/regression/test-sphinx_fe-dct.sh
test/regression/test-sphinx_fe-dither-seed.sh
test/regression/test-sphinx_fe-logspec2cep.sh
test/regression/test-sphinx_fe-logspec.sh
test/regression/test-sphinx_fe.sh
test/regression/test-sphinx_fe-smoothspec.sh
])

chmod +x test/regression/*.sh
